import com.google.gms.googleservices.GoogleServicesPlugin

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "kotlin-allopen"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.tpb.coinz"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'MapBoxAPIKey', MAPBOX_API_KEY
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared"
        }
        androidTest {
            java.srcDirs += "$projectDir/src/testShared"
        }
    }
}

androidExtensions { // For @Parcelable annotation on data classes
    experimental = true
}

allOpen {
    annotation("com.tpb.coinz.OpenForTesting")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    testImplementation 'androidx.test:core:1.0.0'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    androidTestImplementation 'org.mockito:mockito-android:2.21.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.0.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0", {
        exclude group: 'org.mockito', module: 'mockito-core'
    })

    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.0'

    def koin_version = '1.0.2'
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    androidTestImplementation("org.koin:koin-test:$koin_version", {
        exclude group: 'org.mockito'
    })
    testImplementation "org.koin:koin-test:$koin_version"

    // ViewModel and LiveData
    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$lifecycle_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"

    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-firestore:17.1.3'
    implementation 'com.firebaseui:firebase-ui-auth:4.1.0'

    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-fitness:16.0.1'

    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:6.7.2'



    def room_version = "2.0.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test:rules:1.1.1-alpha01'


}


apply plugin: 'com.google.gms.google-services'
GoogleServicesPlugin.config.disableVersionCheck = true
